********************************************************************************
* Mincer regressions
*
* "Firms and the Decline in Earnings Inequality in Brazil"
*
* by Jorge Alvarez (International Monetary Fund),
* Benguria Benguria (University of Kentucky),
* Niklas Engbom (Princeton University), and
* Christian Moser (Columbia University)
*
* This file may be used to reproduce figure A1
*
* First created: 10/09/2014
* Last edited: 06/24/2017
********************************************************************************


* Program description:
* estimates Mincer regressions in MATLAB.
*
*	-	The following run options exist:
*				(1)	$inmatlab: 1 for running in MATLAB, 0 for running in STATA
*				(2)	$byyear: 1 if AKM.do has been run by year, 0 if has been run
*					by period
*
*	-	Loads data generated by AKM.do
*	-	The file name and directory of the input data can be specified in string
*		'r1' at the top of MINCER_BYPERIOD.m and MINCER_BYYEAR.m respectively;
*	-	The TOMATLAB_BYPERIOD_$akm_start_$akm_end.csv files need to contain the 
*		following variables in the following order
*			[ wage persid empid ano edu age occup sector ]
*	-	The TOMATLAB_BYYEAR_'yyyy'.csv files need to contain the following
*		variables in the following order
*			[ wage persid empid edu age occup sector ]
*	-	Note that MINCER_BYPERIOD.m and MINCER_BYYEAR.m estimate the same thing
*		produce the same output and store it in the same file. The difference is
*		only in what data it loads.
*	-	BYYEAR = 1 is the more efficient approach, but it requires that AKM.do
*		has been run by year
*	-	The results for each year are stored in SAVE_MATLAB/mincer_nofirms_yyyy.txt 
*		and SAVE_MATLAB/mincer_withfirms_yyyy.txt. These file names and 
*		directories are controlled by strings 's1' and 's2', respectively, at 
*		the top of MINCER_BYPERIOD.m and MINCER_BYYEAR.m respectively;
*	-	The results are stored in the form of a covariance matrix for the 
*		relevant year.


set more off 
set matsize 800

* Run mincer in MATLAB or in Stata
global inmatlab = 0

if $inmatlab == 0 {
	postfile file year Total AgeEdu_nofirms Occ_nofirms Sec_nofirms Resid_nofirms Nobs_nofirms tss_nofirms rss_nofirms r2_nofirms rmse_nofirms F_nofirms AgeEdu Occ Sec Firm Resid Nobs tss rss r2 rmse F using "$SAVE_SUMMARYSTATS/mincer_${minyear}_${maxyear}.csv", replace
	forvalues yyyy = $minyear/$maxyear {
		disp "*** STARTING YEAR `yyyy' ***"
		
		* Read data for relevant year
		use wage age edu occup clascnae95 empresa_fic using "$SAVE_CLEANED/selection`yyyy'", clear
		scalar year = `yyyy'
		qui tab age, gen(Age)
		qui egen educ = group(edu)
		qui tab educ, gen(Edu)
		qui replace occup = round(occup/10)
		qui egen occ = group(occup)
		qui tab occ, gen(Occup)
		qui egen sec = group(clascnae95)
		qui tab sec, gen(Sector)
		qui foreach var in age educ occ sec {
			qui bys `var': gen n`var' = (_n == 1)
			qui count if n`var'
			qui local d`var' = r(N)
			qui drop n`var'
		}
		qui forvalues i=1/`dage' {
			qui forvalues ii=1/`deduc' {
				qui gen AgeEdu_`i'_`ii' = Age`i'*Edu`ii'
			}
		}
		* drop collinear terms
		drop AgeEdu_1_1 Occup1 Sector1
		
		disp "Regressing Mincer without firms"
		areg wage AgeEdu* Sector*, a(occ)
		
		* Pick up regression results
		scalar Nobs_nofirms = e(N)
		scalar tss_nofirms = e(tss)
		scalar rss_nofirms = e(rss)
		scalar r2_nofirms = e(r2)
		scalar rmse_nofirms = e(rmse)
		scalar F_nofirms = e(F)
		
		* Pick up estimated values and residuals
		qui gen nofirms_AgeEdu = .
		qui forvalues i = 1/`dage' {
			qui forvalues ii = 1/`deduc' {
				qui capture confirm variable AgeEdu_`i'_`ii' 
				qui if _rc==0 replace nofirms_AgeEdu = _b[AgeEdu_`i'_`ii'] if AgeEdu_`i'_`ii' == 1
			}
		}
		qui gen nofirms_Sec = .
		forvalues i = 1/`dsec' {
			qui capture confirm variable Sector`i'
			qui if _rc==0 replace nofirms_Sec = _b[Sector`i'] if Sector`i' == 1
		}
		qui predict nofirms_Resid, resid
		qui predict nofirms_Occ, d
		qui foreach var in wage nofirms_AgeEdu nofirms_Sec nofirms_Occ nofirms_Resid {
			qui sum `var'
			scalar var_`var' = r(Var)
		}
		
		disp "Regressing Mincer with firms"
		areg wage AgeEdu* Occup*, a(empresa_fic)
		
		* Pick up regression results
		scalar Nobs = e(N)
		scalar tss = e(tss)
		scalar rss = e(rss)
		scalar r2 = e(r2)
		scalar rmse = e(rmse)
		scalar F = e(F)

		* Pick up estimated values and residuals
		qui gen AgeEdu = .
		qui forvalues i = 1/`dage' {
			qui forvalues ii = 1/`deduc' {
				qui capture confirm variable AgeEdu_`i'_`ii' 
				qui if _rc==0 replace AgeEdu = _b[AgeEdu_`i'_`ii'] if AgeEdu_`i'_`ii' == 1
			}
		}
		qui gen Occ = .
		forvalues i = 1/`docc' {
			qui capture confirm variable Occup`i'
			qui if _rc==0 replace Occ = _b[Occup`i'] if Occup`i' == 1
		}
		qui predict Resid, resid
		qui predict Firm, d
		qui bys sec: egen Sec = mean(Firm)
		qui replace Firm = Firm-Sec
		qui foreach var in wage AgeEdu Occ Sec Firm Resid {
			qui sum `var'
			scalar var_`var' = r(Var)
		}
		
		* Store results
		qui	post file (year) (var_wage) (var_nofirms_AgeEdu) (var_nofirms_Occ) (var_nofirms_Sec) (var_nofirms_Resid) (Nobs_nofirms) (tss_nofirms) (rss_nofirms) (r2_nofirms) (rmse_nofirms) (F_nofirms) (var_AgeEdu) (var_Occ) (var_Sec) (var_Firm) (var_Resid) (Nobs) (tss) (rss) (r2) (rmse) (F)
	}

	* Close posting file
	postclose file
		
		
	*** Save files in .CSV format
	forvalues yyyy = $minyear/$maxyear {
		disp "*** STARTING YEAR `yyyy' ***"
		* Import results
		use "$SAVE_SUMMARYSTATS/mincer_${minyear}_${maxyear}", clear
		outsheet year Total AgeEdu_nofirms Occ_nofirms Sec_nofirms Resid_nofirms Nobs_nofirms tss_nofirms rss_nofirms r2_nofirms rmse_nofirms F_nofirms AgeEdu Occ Sec Firm Resid Nobs tss rss r2 rmse F using "$SAVE_SUMMARYSTATS/mincer.csv", delimiter(";") nolabel noquote replace
		keep if year == `yyyy'
		foreach var in Total AgeEdu_nofirms Occ_nofirms Sec_nofirms Resid_nofirms Nobs_nofirms tss_nofirms rss_nofirms r2_nofirms rmse_nofirms F_nofirms AgeEdu Occ Sec Firm Resid Nobs tss rss r2 rmse F {
			format `var' %5.4f
		}
		
		* Convert to .CSV format
		outsheet year Total AgeEdu_nofirms Occ_nofirms Sec_nofirms Resid_nofirms Nobs_nofirms tss_nofirms rss_nofirms r2_nofirms rmse_nofirms F_nofirms AgeEdu Occ Sec Firm Resid Nobs tss rss r2 rmse F using "$SAVE_SUMMARYSTATS/mincer`yyyy'.csv", delimiter(";") nolabel noquote replace
	}
	//rm "$SAVE_SUMMARYSTATS/mincer`yyyy'.dta"	
}




else if $inmatlab == 1 {
	* Estimate in MATLAB using MINCER.m. This requires the following variables
	* to be generated by AKM.do and stored in TOMATLAB_BYYEAR_${akm_start}_${akm_end}.csv:
	*	[ wage persid empid edu age occup sector ]
	
	* Run mincer for each period
	if $byperiod == 0 {
		forvalues yyyy = $minyear/$maxyear {
			disp "***** Running MINCER in MATLAB for year `yyyy' *****"
			!matlab -nodesktop -nojvm -nosplash -nodisplay -logfile ${SAVE_MATLAB}/log_MINCER_BYYEAR_${akm_start}_${akm_end} -r "clear all; close all; yyyy = `yyyy'; SAVE_MATLAB = '${SAVE_MATLAB}'; addpath('${MATLABBGLPATH}'); run ${MAINPATH}/MINCER_BYYEAR.m"
			if `yyyy' < $maxyear sleep $sleeper
		}
	}
	else if $byperiod == 1 {
		forvalues yyyy = $minyear/$maxyear {
			foreach start in 1988 1992 1996 2000 2004 2008 {
				if `yyyy' >= `start' local yyyy1 = `start' 
			}
			local yyyy2 = `yyyy1' + $periodlength
			disp "***** Running MINCER in MATLAB for year `yyyy' (period `yyyy1' - `yyyy2') *****"
			!matlab -nodesktop -nojvm -nosplash -nodisplay -logfile ${SAVE_MATLAB}/log_MINCER_BYPERIOD_${akm_start}_${akm_end} -r "clear all; close all; yyyy1 = `yyyy1'; yyyy2 = `yyyy2'; yyyy = `yyyy'; SAVE_MATLAB = '${SAVE_MATLAB}'; addpath('${MATLABBGLPATH}'); run ${MAINPATH}/MINCER_BYPERIOD.m"
			if `yyyy' < $maxyear sleep $sleeper
		}
	}
}

